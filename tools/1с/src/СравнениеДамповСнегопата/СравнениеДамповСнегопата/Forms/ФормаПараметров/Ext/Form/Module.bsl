
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыЗапуска = Параметры.ПараметрыЗапуска;
	
	РазложитьПараметры();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыПриИзменении(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Закрыть(ПараметрыЗапуска);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭлементыПриИзменении(Элемент)
	
	Элементы.Архитектура.ТолькоПросмотр	= Не СтавитьАрхитектура;
	Элементы.ИмяБазы.ТолькоПросмотр		= Не СтавитьИмяБазы;
	Элементы.Логин.ТолькоПросмотр		= Не СтавитьЛогин;
	Элементы.Пароль.ТолькоПросмотр		= Не СтавитьПароль;
	
	СформироватьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НайтиПараметр(КоманднаяСтрока, ИмяПараметра, ИскатьЗначение = Истина)
	
	Результат = Новый Структура("ЕстьПараметр, ЗначениеПараметра", Ложь, "");
	
	поз = СтрНайти(КоманднаяСтрока, ИмяПараметра);
	Если поз > 0 Тогда
		Результат.ЕстьПараметр = Истина;
	КонецЕсли;
	
	Если Не Результат.ЕстьПараметр Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ИскатьЗначение Тогда
		Возврат Результат;
	КонецЕсли;
	
	значениеПараметра = "";
	поискЗавершен = Ложь;
	длинаСтроки = СтрДлина(КоманднаяСтрока);
	позПарам = поз + СтрДлина(ИмяПараметра);
	открытыКавычки = Ложь;
	Если (Сред(КоманднаяСтрока, позПарам, 1) = " ") Тогда
		позПарам = позПарам + 1;
	КонецЕсли;
	Пока поискЗавершен = Ложь и позПарам <= длинаСтроки Цикл
		
		текСимвол = Сред(КоманднаяСтрока, позПарам, 1);
		
		Если текСимвол = " " И Не открытыКавычки Тогда
			поискЗавершен = Истина;
			Прервать;
		ИначеЕсли текСимвол = """" Тогда
			открытыКавычки = Не открытыКавычки;
		Иначе
			значениеПараметра = значениеПараметра + текСимвол;
		КонецЕсли;
		
		позПарам = позПарам + 1;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(значениеПараметра) И Не открытыКавычки Тогда
		Результат.ЗначениеПараметра = значениеПараметра;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура РазложитьПараметры()
	
	СтрокаДляАнализа = СтрШаблон("%1", СокрЛП(ПараметрыЗапуска));
	
	данныеПараметра = НайтиПараметр(СтрокаДляАнализа, "DESIGNER", Ложь);
	СтавитьЗапускКонфигуратора = данныеПараметра.ЕстьПараметр;
	
	данныеПараметра = НайтиПараметр(СтрокаДляАнализа, "/AppArch");
	СтавитьАрхитектура = данныеПараметра.ЕстьПараметр;
	Если СтавитьАрхитектура Тогда
		Архитектура = данныеПараметра.ЗначениеПараметра;
	КонецЕсли;
	
	данныеПараметра = НайтиПараметр(СтрокаДляАнализа, "/IBName");
	СтавитьИмяБазы = данныеПараметра.ЕстьПараметр;
	Если СтавитьИмяБазы Тогда
		ИмяБазы = данныеПараметра.ЗначениеПараметра;
	КонецЕсли;
	
	данныеПараметра = НайтиПараметр(СтрокаДляАнализа, "/N");
	СтавитьЛогин = данныеПараметра.ЕстьПараметр;
	Если СтавитьЛогин Тогда
		Логин = данныеПараметра.ЗначениеПараметра;
	КонецЕсли;
	
	данныеПараметра = НайтиПараметр(СтрокаДляАнализа, "/P");
	СтавитьПароль = данныеПараметра.ЕстьПараметр;
	Если СтавитьПароль Тогда
		Пароль = данныеПараметра.ЗначениеПараметра;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПараметры()
	
	массивПараметров = Новый Массив;
	
	Если СтавитьЗапускКонфигуратора Тогда
		массивПараметров.Добавить("DESIGNER");
	КонецЕсли;
	
	Если СтавитьАрхитектура И ЗначениеЗаполнено(Архитектура) Тогда
		массивПараметров.Добавить(СтрШаблон("/AppArch %1", Архитектура));
	КонецЕсли;
	
	Если СтавитьИмяБазы И ЗначениеЗаполнено(ИмяБазы) Тогда
		массивПараметров.Добавить(СтрШаблон("/IBName ""%1""", ИмяБазы));
	КонецЕсли;
	
	Если СтавитьЛогин И ЗначениеЗаполнено(Логин) Тогда
		массивПараметров.Добавить(СтрШаблон("/N""%1""", Логин));
	КонецЕсли;
	
	Если СтавитьПароль Тогда
		массивПараметров.Добавить(СтрШаблон("/P""%1""", Пароль));
	КонецЕсли;
	
	ПараметрыЗапуска = СтрСоединить(массивПараметров, " ");
	
КонецПроцедуры

#КонецОбласти